plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'cloud.kitelang'
version = '0.2.2'

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId = 'kite-provider-sdk'

            pom {
                name = 'Kite Provider SDK'
                description = 'SDK for building Kite infrastructure providers'
                url = 'https://github.com/kitecorp/kite-provider-sdk'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    // Use 'api' so provider implementors get transitive access
    api project(':kite-proto')
    api project(':kite-api')

    // gRPC Netty transport for provider servers
    api 'io.grpc:grpc-netty-shaded:1.77.0'

    // MessagePack for ResourcePayload serialization
    api 'org.msgpack:jackson-dataformat-msgpack:0.9.10'

    // Jackson for JSON handling
    api 'com.fasterxml.jackson.core:jackson-databind:2.18.2'

    // Lombok for boilerplate reduction
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    // SLF4J logging API
    api 'org.slf4j:slf4j-api:2.0.17'

    // Testing
    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

test {
    useJUnitPlatform()
    // Required for msgpack DirectByteBuffer access in Java 17+
    jvmArgs '--add-opens=java.base/java.nio=ALL-UNNAMED',
            '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED'
}
